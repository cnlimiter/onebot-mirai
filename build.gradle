plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.8.20'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.8.20'
    id 'com.github.johnrengelman.shadow' version '8.1.1'

    id 'net.mamoe.mirai-console' version '2.16.0'
    id 'maven-publish'
}


group = project.maven_group
version = project.version

java {
    archivesBaseName = 'OneBot-Mirai'
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    maven { url 'https://maven.aliyun.com/repository/public' }
    maven {
        url "https://maven.nova-committee.cn/releases"
    }
    maven { url "https://repo.mirai.mamoe.net/keep/mcl" }
    mavenCentral()
}

dependencies {
    implementation('org.java-websocket:Java-WebSocket:1.5.3')
    "shadowLink"("org.java-websocket:Java-WebSocket")
    implementation('cn.evole.bot:OneBot-SDK:0.2.6')
    "shadowLink"("cn.evole.bot:OneBot-SDK")
    implementation('com.google.code.gson:gson:2.10.1')

    compileOnly 'org.projectlombok:lombok:1.18.24'

    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.java-websocket:Java-WebSocket:1.5.3'

    testImplementation("ch.qos.logback:logback-classic:1.2.11")
    testImplementation("org.slf4j:slf4j-simple:2.0.7")
    testImplementation("org.junit.jupiter:junit-jupiter:5.10.0")
    testImplementation("org.mockito:mockito-core:5.4.0")
}

artifacts {
    archives jar
    archives shadowJar
}



mirai {
    jvmTarget = JavaVersion.VERSION_17
    excludeDependency("org.slf4j", "slf4j-api")
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
    options.release.set(17)
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)


            version = "${project.version}"
            artifactId = "${project.archives_base_name}"
            groupId = "${project.maven_group}"
            pom {
                name = 'OneBot Mirai'
                description = '基于 [OneBot] 标准的 mirai 插件版'
                url = 'https://github.com/cnlimiter/onebot-mirai'
                licenses {
                    license {
                        name = 'AGPLV3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'cnlimiter'
                        name = 'cnlimiter'
                        email = 'cnlimiter@163.com'
                    }
                }
            }
        }
    }

    repositories {
        if (System.getenv('MAVEN_USERNAME') != null && System.getenv('MAVEN_PASSWORD') != null) {
            maven {
                name 'release'
                url = 'https://maven.nova-committee.cn/releases'

                credentials {
                    username System.getenv('MAVEN_USERNAME')
                    password System.getenv('MAVEN_PASSWORD')
                }
            }
        }
    }
}
